// Phase 3: population cage simulation

initialize() {
// Cage parameters
defineConstant("K_CAGE", 1000);
defineConstant("NUM_GEN", 100);
defineConstant("N_FOUNDERS_LOW", 1);
defineConstant("N_FOUNDERS_HIGH", 20);
// Chromosome parameters
defineConstant("L", 23e6); 
defineConstant("Rec_rate", 2.4e-8);    
defineConstant("Inv_start", 2e6);
defineConstant("Inv_center_start", 5.5e6);
defineConstant("Inv_center_end", 9.5e6);
defineConstant("Inv_end", 13e6);
defineConstant("Inv_marker", 3e6);   
// Mutation parameters
defineConstant("MU_total", 3e-9);      
defineConstant("DFE_shape", 0.33);     
defineConstant("DFE_mean_s", -0.001);  
defineConstant("H_dominance", 0.0);    
// job id
if (!exists("jobID")) defineConstant("jobID", 1);
setSeed(getSeed() + jobID);
initializeSLiMModelType("nonWF");
// Mutations
initializeMutationType("m1", 0.5, "f", 0.0); 
m1.convertToSubstitution = F;
initializeMutationType("m2", H_dominance, "g", DFE_mean_s, DFE_shape); 
m2.convertToSubstitution = F;
initializeMutationType("m3", 0.5, "f", 0.0); 
m3.convertToSubstitution = F;
// Genome
initializeGenomicElementType("g1", c(m1, m2), c(0.26, 0.74));
initializeGenomicElement(g1, 0, L-1);
initializeMutationRate(MU_total);
initializeRecombinationRate(Rec_rate);
}

1 early() {
sim.readFromPopulationFile(BURN_IN_FILE); // Read burnin
defineConstant("START_GEN", sim.cycle);
sim.addSubpop("p2", 0); // LF Cage 
sim.addSubpop("p3", 0); // MF Cage 
inds_HET = p1.individuals[p1.individuals.countOfMutationsOfType(m3) == 1]; // Pick heterozygotes
p2.takeMigrants(sample(inds_HET, N_FOUNDERS_LOW));
p3.takeMigrants(sample(inds_HET, N_FOUNDERS_HIGH));
sim.killIndividuals(p1.individuals); 
p1.removeSubpopulation(); // Remove p1
catn("Phase 3 Started at Cycle: " + START_GEN); // Log
community.rescheduleScriptBlock(s1, start=START_GEN, end=START_GEN + NUM_GEN);
}

// Reproduction
reproduction(p1) { self.active = 0; }
reproduction(p2) {
for (i in 1:(K_CAGE)) { // K_cage offsprings per generation
mate1 = p2.sampleIndividuals(1);
mate2 = p2.sampleIndividuals(1);
p2.addCrossed(mate1, mate2);
}
self.active = 0;
}
reproduction(p3) {
for (i in 1:(K_CAGE)) {
mate1 = p3.sampleIndividuals(1);
mate2 = p3.sampleIndividuals(1);
p3.addCrossed(mate1, mate2);
}
self.active = 0;
}

// Death density-control
early() {
if (sim.cycle == START_GEN) return;
if (p2.individualCount > 0) p2.fitnessScaling = K_CAGE / p2.individualCount;
if (p3.individualCount > 0) p3.fitnessScaling = K_CAGE / p3.individualCount;
}

// Recombination
recombination() {
// If have same arrangements, normal recombination
if (haplosome1.containsMarkerMutation(m3, Inv_marker) == haplosome2.containsMarkerMutation(m3, Inv_marker))
return F;
// 
inInv = (breakpoints > Inv_start) & (breakpoints < Inv_end);
inCenter = (breakpoints > Inv_center_start) & (breakpoints < Inv_center_end);
Rec_inCenter = breakpoints[inCenter];
// If no cross over within inversion, normal recombination
if (!any(inInv))
return F;
// If crossover happen within inversions but outside center, supressed recombination
if (any(inInv & !inCenter)) {
breakpoints = breakpoints[!inInv];
return T;
}
// If odd numbers of crossovers happen within inversion center, supressed recombination
if (size(Rec_inCenter) % 2 == 1) {
breakpoints = breakpoints[!inInv];
return T;
} 
// If even numbers of crossovers happen with inversion center, normal recombination
return F;
}

// Tracking
s1 late() {
gensSinceStart = sim.cycle - START_GEN;
if (gensSinceStart % 1 == 0) {
populations = c(p2, p3);
labels = c("LF", "MF");
for (i in seqAlong(populations)) {
sub = populations[i];
label = labels[i];
n = sub.individualCount;
if (n > 0) {
m3_counts = sub.individuals.countOfMutationsOfType(m3);
f_std = sum(m3_counts == 0) / n;
f_het = sum(m3_counts == 1) / n;
f_inv = sum(m3_counts == 2) / n;
catn("Gen " + gensSinceStart + " | Cage: " + label + 
" | STD: " + format("%.3f", f_std) + 
" | HET: " + format("%.3f", f_het) + 
" | INV: " + format("%.3f", f_inv));
}
}
}
if (gensSinceStart >= NUM_GEN) {
catn("Aim 3 Simulation Complete.");
sim.simulationFinished();
}
}











